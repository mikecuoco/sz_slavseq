import pandas as pd
from pathlib import Path
from snakemake.utils import validate, min_version, Paramspace


##### set minimum snakemake version #####
min_version("7.22.0")


configfile: "config/config.yml"


validate(config, schema="schemas/config.schema.yaml")

# read sample sheets
samples = pd.read_csv(
    config["samples"],
    sep="\t",
    dtype={"sample_id": str, "tissue_id": str, "donor_id": str},
)
validate(samples, schema="schemas/samples.schema.yaml")
donors = pd.read_csv(config["donors"], sep="\t", dtype={"donor_id": str})
validate(donors, schema="schemas/donors.schema.yaml")

# merge sample sheets
samples = samples.merge(donors, on=["donor_id"]).set_index(
    ["donor_id", "sample_id"], drop=False
)

# create donor sheet
donors = donors.set_index("donor_id", drop=False)

# setup parameters for regions
regions_params = []
for wsize in [100, 200, 300]:
    for bgtest in [True, False]:
        for bgsize in [10000, 20000]:
            regions_params.append(
                {
                    "mode": "peaks",
                    "step": 1,
                    "minreads": 3,
                    "size": wsize,
                    "bgtest": bgtest,
                }
            )
            if bgtest:
                regions_params[-1]["bgsize"] = int(bgsize)

regions_params = Paramspace(pd.DataFrame(regions_params))


wildcard_constraints:
    donor="|".join(donors["donor_id"]),
    sample="|".join(samples["sample_id"]),
    genome=config["genome"]["name"],
    outdir=config["outdir"],
    params="|".join(regions_params.instance_patterns),


include: "rules/ref.smk"
include: "rules/download.smk"
include: "rules/trim.smk"
include: "rules/align.smk"
include: "rules/model.smk"


rule all:
    input:
        expand(
            rules.tune.output,
            genome=config["genome"]["name"],
            params=regions_params.instance_patterns,
            outdir=config["outdir"],
        ),
        expand(
            expand(
                rules.flagstat.output,
                zip,
                donor=samples["donor_id"],
                sample=samples["sample_id"],
                allow_missing=True,
            ),
            genome=config["genome"]["name"],
            outdir=config["outdir"],
        ),
