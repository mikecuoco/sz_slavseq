from snakemake.utils import min_version
from snakemake.remote import FTP, HTTP

FTP = FTP.RemoteProvider()
HTTP = HTTP.RemoteProvider()


##### set minimum snakemake version #####
min_version("7.22.0")


wildcard_constraints:
    sample="\w+",


configfile: "config/config.yml"


include: "rules/common.smk"
include: "rules/ref.smk"
include: "rules/download.smk"
include: "rules/trim.smk"
include: "rules/align.smk"
include: "rules/model.smk"
include: "rules/qc.smk"


rule map:
    input:
        expand(
            expand(
                rules.sambamba_index.output,
                outdir=config["outdir"],
                allow_missing=True,
            ),
            zip,
            sample=samples["sample_id"],
            donor=samples["donor_id"],
        ),
        expand(
            rules.reads_multiqc.output,
            outdir=config["outdir"],
        ),
        expand(
            rules.aln_multiqc.output,
            outdir=config["outdir"],
        ),


rule label:
    input:
        expand(
            rules.get_labels.output,
            donor=donors.loc[~donors["KNRGL"].isnull(), "donor_id"],
            outdir=config["outdir"],
        ),


rule train:
    input:
        expand(
            rules.fit.output,
            outdir=config["outdir"],
        ),


rule call:
    input:
        expand(
            expand(
                rules.predict.output,
                zip,
                sample=samples.loc[samples["donor_id"] != "CommonBrain", :][
                    "sample_id"
                ],
                donor=samples.loc[samples["donor_id"] != "CommonBrain", :]["donor_id"],
                allow_missing=True,
            ),
            outdir=config["outdir"],
        ),
